//noinspection GroovyAssignabilityCheck

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.5.0.1'
    }
    ext {
        lombokVersion = '1.18.22'
        springCloudStarterVersion = '2.2.8.RELEASE'
        swaggerVersion = '2.9.2'
        mapStructVersion = '1.4.2.Final'
        karateVersion = '1.1.0'
        spockVersion = '2.0-groovy-2.5'
        restAssuredVersion = '4.2.1'
        assertjVersion = '3.22.0'
        auth0JavaJwtVersion = '3.18.2'
        testcontainersVersion = '1.16.2'
    }
}

plugins {
    id 'org.springframework.boot' version "2.3.12.RELEASE"
    id "org.springframework.cloud.contract" version "2.1.5.RELEASE"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'java'
    id 'war'
    id 'groovy'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
    id "org.liquibase.gradle" version "2.1.0"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'

group = 'org.bookworm'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.cloud:spring-cloud-starter-config:${springCloudStarterVersion}"
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${springCloudStarterVersion}"
    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.security:spring-security-test"
    implementation "com.auth0:java-jwt:${auth0JavaJwtVersion}"
    implementation "com.auth0:jwks-rsa:0.20.0"
    implementation "org.zalando:logbook-spring-boot-starter:2.14.0"
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "net.minidev:json-smart:2.4.7"
    implementation "com.google.guava:guava:31.0.1-jre"
    implementation "org.codehaus.groovy:groovy:2.5.15"
    implementation "com.googlecode.json-simple:json-simple:1.1.1"
    implementation "org.liquibase:liquibase-core:4.4.3"
    compileOnly "org.liquibase:liquibase-gradle-plugin:2.0.4"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly "org.postgresql:postgresql:42.3.1"
    runtimeOnly "org.liquibase.ext:liquibase-hibernate5:4.7.1"
    runtimeOnly "io.micrometer:micrometer-registry-prometheus:1.7.6"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.1.0"
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation "com.intuit.karate:karate-core:${karateVersion}"
    testImplementation "com.intuit.karate:karate-junit5:${karateVersion}"
    testImplementation platform("org.spockframework:spock-bom:${spockVersion}")
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    testImplementation "io.rest-assured:spring-mock-mvc:${restAssuredVersion}"
    testImplementation "io.rest-assured:json-path:${restAssuredVersion}"
    testImplementation "io.rest-assured:xml-path:${restAssuredVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
}

bootJar {
    layered()
    manifest {
        attributes 'Start-Class': 'org.bookworm.library.LibraryApplication'
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "org.bookworm:bookworm-backend"
        property "sonar.projectName", "bookworm-backend"
        property "sonar.coverage.exclusions", "src/main/java/org/bookworm/library/dto/**/*, src/main/java/org/bookworm/library/entities/**/*, src/main/java/org/bookworm/library/security/**/*, src/main/java/org/bookworm/library/*"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.language", "java"
        property "sonar.binaries", "build/classes"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/*"
        property "sonar.verbose", "true"
        property "sonar.log.level", "TRACE"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

test {
    useJUnitPlatform()
    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    outputs.upToDateWhen { false }
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification

task karateDebug(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.intuit.karate.cli.Main'
}

task printSourceSetInformation() {
    doLast {
        sourceSets.each { srcSet ->
            println "[" + srcSet.name + "]"
            print "-->Source directories: " + srcSet.allJava.srcDirs + "\n"
            print "-->Output directories: " + srcSet.output.classesDirs.files + "\n"
//            print "-->Compile classpath:\n"
//            srcSet.compileClasspath.files.each {
//                print "  " + it.path + "\n"
//            }
            println ""
        }
    }
}
